import org.gradle.api.artifacts.maven.MavenDeployment

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'signing'

group = 'no.entitas'
version = '1.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    testCompile 'org.spockframework:spock-core:0.5-groovy-1.8'
    groovy "org.codehaus.groovy:groovy-all:1.8.0"
}

task sourcesJar(type: Jar, dependsOn: classes) {
     classifier = 'sources'
     from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
     classifier = 'javadoc'
     from javadoc.destinationDir
}

artifacts {
     archives sourcesJar
     archives javadocJar
}

signing {
    sign configurations.archives
}

defaultProject = {
    name 'Gradle JAXB plugin'
    // packaging 'jar' (Packaging is set later through direct modification of the xml)
    description 'The Gradle JAXB plugin creates Java classes from XSD schemas.'
    url 'http://github.com/stianh/gradle-jaxb-plugin'
    inceptionYear '2011'

    scm {
        developerConnection 'git@github.com:stianh/gradle-jaxb-plugin.git'
        connection 'git://github.com/stianh/gradle-jaxb-plugin.git'
        url 'https://github.com/stianh/gradle-jaxb-plugin'
    }

    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }

    developers {
        developer {
            id 'stianh'
            name 'Stian Hegglund'
            email 'stian.hegglund@gmail.com'
            roles {
                role 'Developer'
            }
            timezone '+1'
        }
    }
}

fixedProject = {
    project defaultProject
    withXml {
        def project = it.asNode()
        project.version[0] + {
            packaging 'jar'
        }
    }
}

install.repositories.mavenInstaller {
    pom fixedProject

    beforeDeployment {
        MavenDeployment deployment -> signPom(deployment)
    }
}

uploadArchives.repositories.mavenDeployer {
    pom fixedProject
    uniqueVersion = false

    beforeDeployment {
        MavenDeployment deployment -> signPom(deployment)
    }

    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
    }

    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
    }
}